Grails控制器支持声明式异常处理的简单机制。如果一个控制器声明了一个接受单个参数的方法，并且参数类型是 `java.lang.Exception` 或其某个子类，那么每当该控制器中的一个动作抛出该类型的异常时，该方法将被调用。请看下面的示例。

[src,groovy]
.grails-app/controllers/demo/DemoController.groovy
----
package demo

class DemoController {

    def someAction() {
        // 执行一些工作
    }

    def handleSQLException(SQLException e) {
        render '已处理一个SQLException'
    }

    def handleBatchUpdateException(BatchUpdateException e) {
        redirect controller: 'logging', action: 'batchProblem'
    }

    def handleNumberFormatException(NumberFormatException nfe) {
        [problemDescription: '一个数字是无效的']
    }
}
----

该控制器的行为将类似于以下方式写的...

[src,groovy]
.grails-app/controllers/demo/DemoController.groovy
----
package demo

class DemoController {

    def someAction() {
        try {
            // 执行一些工作
        } catch (BatchUpdateException e) {
            return handleBatchUpdateException(e)
        } catch (SQLException e) {
            return handleSQLException(e)
        } catch (NumberFormatException e) {
            return handleNumberFormatException(e)
        }
    }

    def handleSQLException(SQLException e) {
        render '已处理一个SQLException'
    }

    def handleBatchUpdateException(BatchUpdateException e) {
        redirect controller: 'logging', action: 'batchProblem'
    }

    def handleNumberFormatException(NumberFormatException nfe) {
        [problemDescription: '一个数字是无效的']
    }
}
----

异常处理方法的名称可以是任何有效的方法名。方法之所以成为异常处理器并不是因为其名称，而是因为 `Exception` 参数类型是重要的部分。

异常处理方法可以做与控制器动作相同的任何事情，包括调用 `render` ， `redirect` ，返回一个模型等。

跨多个控制器分享异常处理方法的一种方法是使用继承。异常处理方法会继承到子类，因此应用程序可以在一个抽象类中定义异常处理器，多个控制器从中继承。另一种跨多个控制器分享异常处理方法的方式是使用trait，如下所示...

[src,groovy]
.src/main/groovy/com/demo/DatabaseExceptionHandler.groovy
----
package com.demo

trait DatabaseExceptionHandler {
    def handleSQLException(SQLException e) {
        // 处理SQLException
    }

    def handleBatchUpdateException(BatchUpdateException e) {
        // 处理BatchUpdateException
    }
}
----

[src,groovy]
.grails-app/controllers/com/demo/DemoController.groovy
----
package com.demo

class DemoController implements DatabaseExceptionHandler {

    // 所有在DatabaseExceptionHandler中定义的异常处理器方法将会在编译时添加到这个类中
}
----

异常处理方法必须在编译时存在。具体来说，在运行时编程到一个控制器类的异常处理方法是不受支持的。