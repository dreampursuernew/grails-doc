Grails 控制器支持命令对象的概念。命令对象是一个类，通常与 link:theWebLayer.html#dataBinding[数据绑定] 结合使用，通常用于验证可能不适合现有领域类的数据。

提示：当一个类被用作操作的参数时，它才被视为命令对象。

声明命令对象

命令对象类的定义与任何其他类一样。

```groovy
class LoginCommand implements grails.validation.Validateable {
    String username
    String password

    static constraints = {
        username(blank: false, minSize: 6)
        password(blank: false, minSize: 6)
    }
}
```

在这个例子中，命令对象类实现了 `Validateable` trait。 `Validateable` trait 允许定义 link:validation.html#constraints[约束]，就像 link:GORM.html[领域类] 中一样。如果命令对象在与正在使用它的控制器相同的源文件中定义，Grails 将自动使其成为 `Validateable` 。并非命令对象类必须是可验证的。

默认情况下，所有不是 `java.util.Collection` 或 `java.util.Map` 实例的 `Validateable` 对象属性都是 `nullable: false` 。 `java.util.Collection` 和 `java.util.Map` 的实例默认为 `nullable: true` 。如果要创建具有默认为 `nullable: true` 属性的 `Validateable` 对象，则可以通过在类中定义 `defaultNullable` 方法来指定：

```groovy
class AuthorSearchCommand implements grails.validation.Validateable {
    String  name
    Integer age

    static boolean defaultNullable() {
        true
    }
}
```

在这个例子中， `name` 和 `age` 在验证期间将允许空值。

使用命令对象

为使用命令对象，控制器操作可以选择性地指定任意数量的命令对象参数。必须提供参数类型，以便 Grails 知道要创建和初始化哪些对象。

在执行控制器操作之前，Grails 将自动创建命令对象类的实例，并通过绑定请求参数来填充其属性。如果命令对象类标记为 `Validateable` ，则会对命令对象进行验证。例如：

```groovy
class LoginController {

    def login(LoginCommand cmd) {
        if (cmd.hasErrors()) {
            redirect(action: 'loginForm')
            return
        }

        // 处理命令对象数据
    }
}
```

如果命令对象的类型是领域类，并且存在 `id` 请求参数，则不会调用领域类构造函数创建一个新实例，而是会调用领域类的静态 `get` 方法，并将 `id` 参数的值作为参数传递。

无论从 `get` 方法返回的是什么，都将传递给控制器操作。这意味着，如果存在 `id` 请求参数，但在数据库中找不到相应记录，则命令对象的值将是 `null` 。如果从数据库检索实例时发生错误，则将 `null` 作为参数传递给控制器操作，并将错误添加到控制器的 `errors` 属性中。

如果命令对象的类型是一个领域类，并且没有 `id` 请求参数，或者有一个 `id` 请求参数且其值为空，则除非 HTTP 请求方法是“POST”，否则将 `null` 传递给控制器操作，在这种情况下，通过调用领域类构造函数将创建一个新实例。对于领域类实例非空的所有情况，只有在 HTTP 请求方法是“POST”、“PUT”或“PATCH”时才执行数据绑定。

命令对象和请求参数名称

通常，请求参数名称将直接映射到命令对象中的属性名称。可以使用嵌套参数名称以直观方式绑定对象图。

在下面的示例中，名为 `name` 的请求参数将绑定到 `Person` 实例的 `name` 属性，名为 `address.city` 的请求参数将绑定到 `Person` 的 `address` 属性中的 `city` 属性。

```groovy
class StoreController {
    def buy(Person buyer) {
        // ...
    }
}

class Person {
    String name
    Address address
}

class Address {
    String city
}
```

如果一个控制器操作接受包含相同属性名称的多个命令对象时，可能会出现问题。考虑以下例子。

```groovy
class StoreController {
    def buy(Person buyer, Product product) {
        // ...
    }
}

class Person {
    String name
    Address address
}

class Address {
    String city
}

class Product {
    String name
}
```

如果有一个名为 `name` 的请求参数，不清楚它是表示 `Product` 的名称还是 `Person` 的名称。如果一个控制器操作接受两个相同类型的命令对象，则可能会出现问题。

```groovy
class StoreController {
    def buy(Person buyer, Person seller, Product product) {
        // ...
    }
}

class Person {
    String name
    Address address
}

class Address {
    String city
}

class Product {
    String name
}
```

为了应对这个问题，框架对将参数名称映射到命令对象类型施加了特殊规则。命令对象数据绑定将对所有以控制器操作参数名称开头的参数视为属于相应的命令对象。

例如， `product.name` 请求参数将绑定到 `product` 参数中的 `name` 属性， `buyer.name` 请求参数将绑定到 `buyer` 参数中的 `name` 属性， `seller.address.city` 请求参数将绑定到 `seller` 参数的 `address` 属性的 `city` 属性，等等...

命令对象和依赖注入

命令对象可以参与依赖注入。如果您的命令对象具有一些自定义验证逻辑，需要使用 Grails link:services.html[service]，则这将非常有用：

```groovy
class LoginCommand implements grails.validation.Validateable {

    def loginService

    String username
    String password

    static constraints = {
        username validator: { val, obj ->
            obj.loginService.canLogin(obj.username, obj.password)
        }
    }
}
```

在这个例子中，命令对象与从 Spring `ApplicationContext` 中按名称注入的 `loginService` bean 交互。

将请求体绑定到命令对象

当向接受命令对象的控制器操作发出请求且请求包含主体时，Grails 将尝试基于请求内容类型解析请求体，并使用该主体对命令对象进行数据绑定。请参考下面的例子。

```groovy
package bindingdemo

class DemoController {

    def createWidget(Widget w) {
        render "Name: ${w?.name}, Size: ${w?.size}"
    }
}

class Widget {
    String name
    Integer size
}
```

```bash
$ curl -H "Content-Type: application/json" -d '{"name":"Some Widget","42"}'[size] localhost:8080/demo/createWidget
 Name: Some Widget, Size: 42

$ curl -H "Content-Type: application/xml" -d '<widget><name>Some Other Widget</name><size>2112</size></widget>' localhost:8080/bodybind/demo/createWidget
 Name: Some Other Widget, Size: 2112
```

【注意】
请求体将不会在以下情况下解析：
- 请求方法是 GET
- 请求方法是 DELETE
- 内容长度为 0

请注意，请求体正在解析以使其工作。之后任何尝试读取请求体的操作都将失败，因为相应的输入流将为空。控制器操作可以使用命令对象，也可以自行解析请求体（直接或通过引用诸如 request.JSON 之类的内容），但不能同时执行两者。

处理命令对象列表

命令对象的常见用例是包含另一个集合的命令对象：

```groovy
class DemoController {

    def createAuthor(AuthorCommand command) {
        // ...
    }

    class AuthorCommand {
        String fullName
        List<BookCommand> books
    }

    class BookCommand {
        String title
        String isbn
    }
}
```

在此示例中，我们要创建一个带有多个书籍的作者。

为了使此操作从UI层正常工作，可以在您的 GSP 中执行以下操作：

```xml
<g:form name="submit-author-books" controller="demo" action="createAuthor">
    <g:fieldValue name="fullName" value=""/>
    <ul>
        <li>
            <g:fieldValue name="books[0].title" value=""/>
            <g:fieldValue name="books[0].isbn" value=""/>
        </li>

        <li>
            <g:fieldValue name="books[1].title" value=""/>
            <g:fieldValue name="books[1].isbn" value=""/>
        </li>
    </ul>
</g:form>
```

还支持 JSON，因此您可以提交以下内容以正确地绑定数据：

```json
{
    "fullName": "Graeme Rocher",
    "books": [{
        "title": "The Definitive Guide to Grails",
        "isbn": "1111-343455-1111"
    }, {
        "title": "The Definitive Guide to Grails 2",
        "isbn": "1111-343455-1112"
    }],
}
```