==== 返回模型

模型是视图在渲染时使用的Map。在该Map中的键对应于视图可访问的变量名。有几种方法可以返回一个模型。首先，你可以显式地返回一个Map实例：

[source,groovy]
----
def show() {
    [book: Book.get(params.id)]
}
----

注意：上述内容 _并不_ 反映您应该在脚手架视图中使用的内容-请查看链接：scaffolding.html[scaffolding部分]了解更多详情。

一个更高级的方法是返回Spring {springapi}org/springframework/web/servlet/ModelAndView.html[ModelAndView]类的实例：

[source,groovy]
----
import org.springframework.web.servlet.ModelAndView

def index() {
    // 为首页获取一些书籍，也许是您的收藏夹内容
    def favoriteBooks = ...

    // 转发到列表视图以展示它们
    return new ModelAndView("/book/list", [ bookList : favoriteBooks ])
}
----

需要记住的一件事是，某些变量名不能在您的模型中使用：

* `attributes`
* `application`

目前，如果您使用它们，系统不会报告错误，但希望在Grails的将来版本中会更改。

==== 选择视图

在前面的两个示例中，没有指定要渲染的 <<gsp,视图>> 的代码。那么Grails如何知道应该选择哪一个？答案在于约定。Grails会在 `grails-app/views/book/show.gsp` 位置查找该 `show` 动作的视图：

[source,groovy]
----
class BookController {
    def show() {
         [book: Book.get(params.id)]
    }
}
----

要渲染不同的视图，使用链接:../ref/Controllers/render.html[render]方法：

[source,groovy]
----
def show() {
    def map = [book: Book.get(params.id)]
    render(view: "display", model: map)
}
----

在这种情况下，Grails将尝试在 `grails-app/views/book/display.gsp` 位置渲染一个视图。注意，Grails会自动使用 `grails-app/views` 目录中的 `book` 目录对视图位置进行限定。这很方便，但要访问共享视图，您应该使用绝对路径而不是相对路径：

[source,groovy]
----
def show() {
    def map = [book: Book.get(params.id)]
    render(view: "/shared/display", model: map)
}
----

在这种情况下，Grails将尝试在 `grails-app/views/shared/display.gsp` 位置渲染一个视图。

Grails还支持JSP作为视图，因此如果未在预期位置找到GSP但找到了JSP，它将使用JSP。

[注意]
====

与GSP不同，JSP必须位于目录路径 `/src/main/webapp/WEB-INF/grails-app/views` 中。

另外，为确保JSP正常工作，请不要忽略在您的 `build.gradle` 文件中包含JSP和JSTL实现所需的依赖项。

==== 为有命名空间的控制器选择视图

如果控制器使用<<namespacedControllers,namespace>>属性为自身定义了命名空间，那将影响Grails在何处查找使用相对路径指定的视图的根目录。以带有命名空间的控制器渲染的视图的默认根目录为 `grails-app/views/<命名空间名称>/<控制器名称>/` 。如果在有命名空间的目录中找不到视图，那么Grails将会回退到非命名空间目录中查找视图。

请参阅下面的示例。

[source,groovy]
----
class ReportingController {
    static namespace = 'business'

    def humanResources() {
        // 如果存在，则将呈现grails-app/views/business/reporting/humanResources.gsp
        // 如果grails-app/views/business/reporting/humanResources.gsp不存在，则会回退到grails-app/views/reporting/humanResources.gsp

        // 有命名空间的GSP将优先于非命名空间的GSP。
        [numberOfEmployees: 9]
    }



    def accountsReceivable() {
        // 如果存在则将呈现grails-app/views/business/reporting/numberCrunch.gsp
        // 如果grails-app/views/business/reporting/numberCrunch.gsp不存在，则会回退到grails-app/views/reporting/numberCrunch.gsp

        // 有命名空间的GSP将优先于非命名空间的GSP。
        render view: 'numberCrunch', model: [numberOfEmployees: 13]
    }
}
----

==== 渲染响应

有时（例如在Ajax应用程序中），直接从控制器向响应中渲染文本或代码片段会更容易。为此，可以使用非常灵活的 `render` 方法：

[source,groovy]
----
render "Hello World!"
----

上述代码将文本“Hello World!”写入响应。其他示例包括：

[source,groovy]
----
// 写一些标记
render {
   for (b in books) {
      div(id: b.id, b.title)
   }
}
----

[source,groovy]
----
// 渲染特定视图
render(view: 'show')
----

[source,groovy]
----
// 为集合中的每个项目渲染模板
render(template: 'book_template', collection: Book.list())
----

[source,groovy]
----
// 以编码和内容类型渲染一些文本
render(text: "<xml>some xml</xml>", contentType: "text/xml", encoding: "UTF-8")
----

如果您打算使用Groovy的 `MarkupBuilder` 生成与 `render` 方法一起使用的HTML，请注意HTML元素和Grails标记之间的命名冲突，例如：

[source,groovy]
----
import groovy.xml.MarkupBuilder
...
def login() {
    def writer = new StringWriter()
    def builder = new MarkupBuilder(writer)
    builder.html {
        head {
            title 'Log in'
        }
        body {
            h1 'Hello'
            form {
            }
        }
    }

    def html = writer.toString()
    render html
}
----

实际上这将 <<tagsAsMethodCalls,调用form标签>>（这将返回一些文本， `MarkupBuilder` 将忽略）。要正确输出 `<form>` 元素，请使用以下方式：

[source,groovy]
----
def login() {
    // ...
    body {
        h1 'Hello'
        builder.form {
        }
    }
    // ...
}
----