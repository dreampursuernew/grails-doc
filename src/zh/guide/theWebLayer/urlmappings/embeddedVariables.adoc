==== 简单变量

前一节演示了如何将简单的URL映射到具体的"标记"上。在URL映射中，标记是每个斜杠'/'之间的字符序列。具体的标记是明确定义的，比如`/product`。然而，在许多情况下，在运行时你不知道特定标记的值是什么。这种情况下，你可以在URL中使用变量占位符，例如：

```groovy
static mappings = {
  "/product/$id"(controller: "product")
}
```

在这种情况下，通过将$id变量嵌入为第二个标记，Grails会自动将第二个标记映射到一个参数（可以通过`params`对象获得）。例如，给定URL`/product/MacBook`，以下代码将在响应中呈现"MacBook"：

```groovy
class ProductController {
     def index() { render params.id }
}
```

当然，你可以构造更复杂的映射示例。例如，传统的博客URL格式可以映射如下：

```groovy
static mappings = {
   "/$blog/$year/$month/$day/$id"(controller: "blog", action: "show")
}
```

上述映射可让你做诸如：

```
/graemerocher/2007/01/10/my_funky_blog_entry
```

URL中的每个标记再次映射到`params`对象，可用于`year`、`month`、`day`、`id`等值。

==== 动态控制器和动作名称

变量还可以用于动态构建控制器和动作名称。实际上，默认的Grails URL映射使用了这种技术：

```groovy
static mappings = {
    "/$controller/$action?/$id?"()
}
```

在这里，控制器、动作和id的名称隐式地从嵌入在URL中的`controller`、`action`和`id`变量中获得。

你还可以通过闭包解析控制器名称和动作名称以动态执行：

```groovy
static mappings = {
    "/$controller" {
        action = { params.goHere }
    }
}
```

==== 可选变量

默认映射的另一个特征是在变量末尾附加`?`来使其成为可选标记。在进一步的示例中，这种技术可以应用于博客URL映射以获得更灵活的链接：

```groovy
static mappings = {
    "/$blog/$year?/$month?/$day?/$id?"(controller:"blog", action:"show")
}
```

通过这种映射，所有这些URL都将匹配，并且仅相关参数将填充在`params`对象中：

```
/graemerocher/2007/01/10/my_funky_blog_entry
/graemerocher/2007/01/10
/graemerocher/2007/01
/graemerocher/2007
/graemerocher
```

==== 可选文件扩展名

如果你希望捕获特定路径的扩展名，则存在一种特殊情况的映射：

```groovy
"/$controller/$action?/$id?(.$format)?"()
```

通过添加`(.$format)?`映射，你可以在控制器中使用`response.format`属性来访问文件扩展名：

```groovy
def index() {
    render "扩展名是 ${response.format}"
}
```

==== 任意变量

你还可以通过在传递给映射的块中设置它们来将任意参数从URL映射传递到控制器：

```groovy
"/holiday/win" {
     id = "Marrakech"
     year = 2007
}
```

这些变量将在传递给控制器的`params`对象中可用。

==== 动态解析变量

硬编码的任意变量很有用，但有时你需要根据运行时因素计算变量名称。通过将一个块分配给变量名也是可以的：

```groovy
"/holiday/win" {
     id = { params.id }
     isEligible = { session.user != null } // 必须已登录
}
```

在上述情况下，块内的代码在URL实际匹配时解析，因此可以与各种逻辑结合使用。