对于更复杂的编组器，建议您实现 {apiDocs}org/grails/web/converters/marshaller/ObjectMarshaller.html[ObjectMarshaller]接口。例如，给定一个领域类：

[source,groovy]
----
class Book {
    String title
}
----

默认情况下，使用以下代码时的输出：

[source,groovy]
----
render book as XML
----

将如下所示：

[source,xml]
----
<book id="1">
   <title>The Stand</title>
</book>
----

要编写自定义编组器，您可以执行以下操作：

[source,groovy]
----
class BookMarshaller implements ObjectMarshaller<XML> {

  public boolean supports(Object object) {
    return object instanceof Book
  }

  public void marshalObject(Object object, XML converter) {
    Book book  = (Book)object
    converter.chars book.title
  }
}
----

然后通过以下方式注册编组器：

[source,groovy]
----
XML.registerObjectMarshaller(new BookMarshaller())
----

有了自定义的ObjectMarshaller，现在的输出为：

[source,xml]
----
<book>The Stand</book>
----


==== 自定义根元素的名称


如果您希望自定义周围元素的名称，可以实现 {apiDocs}org/grails/web/converters/marshaller/NameAwareMarshaller.html[NameAwareMarshaller]：

[source,groovy]
----
class BookMarshaller implements ObjectMarshaller<XML>,NameAwareMarshaller {

  ...

  String getElementName(Object o) {
    return 'custom-book'
  }

}
----

通过以上更改，输出现在将是：

[source,xml]
----
<custom-book>The Stand</custom-book>
----


==== 使用转换器API或构建器输出标记


通过传递的Converter对象，您可以明确编写转换器API代码以将标记流式传输到响应：

[source,groovy]
----
public void marshalObject(Object object, XML converter) {
  Book book = (Book)object

  converter.attribute 'id', book.id.toString()
  converter.attribute 'date-released', book.dateReleased.toString()

  converter.startNode 'title'
  converter.chars book.title
  converter.end()

}
----

上述代码将产生：

[source,xml]
----
<book id="1" date-released="...">
   <title>The Stand</title>
</book>
----

您还可以使用构建器符号来实现类似的结果（尽管构建器符号对于 `CompileStatic` 无效）：

[source,groovy]
----
public void marshalObject(Object object, XML converter) {
  Book b = (Book)object

  converter.build {
     book(id: b.id) {
        title b.title
     }
  }


==== 使用convertAnother方法递归转换对象


为了创建更复杂的响应，您可以使用 `convertAnother` 方法转换关联和其他对象：

[source,groovy]
----
public void marshalObject(Object object, XML converter) {
  Book book = (Book)object

  converter.startNode 'title'
  converter.chars book.title
  converter.end()

  if (book.authors) {
    converter.startNode 'authors'
    for(author in book.authors) {
      converter.convertAnother author
    }
    converter.end()
  }
}
----