如果您想要更多地控制呈现或者偏好使用自己的编组技术，那么您可以实现自己的 `Renderer` 实例。 下面是一个简单的实现示例，定制了 `Book` 类的呈现：

```groovy
package myapp
import grails.rest.render.*
import grails.web.mime.MimeType

class BookXmlRenderer extends AbstractRenderer<Book> {
    BookXmlRenderer() {
        super(Book, [MimeType.XML,MimeType.TEXT_XML] as MimeType[])
    }

    void render(Book object, RenderContext context) {
        context.contentType = MimeType.XML.name

        def xml = new groovy.xml.MarkupBuilder(context.writer)
        xml.book(id: object.id, title:object.title)
    }
}
```

`AbstractRenderer` 超类具有一个构造函数，接受要呈现的类和接受的 `MimeType` （通过 ACCEPT 头或文件扩展名）的渲染器。

要配置此呈现器，只需将其添加为一个 bean 到 `grails-app/conf/spring/resources.groovy` ：

```groovy
beans = {
    bookRenderer(myapp.BookXmlRenderer)
}
```

结果是所有 `Book` 实例将以以下格式呈现：

```groovy
<book id="1" title="The Stand"/>
```

注意：如果将呈现更改为像上面的完全不同的格式，则还需要更改绑定，如果计划支持 POST 和 PUT 请求。 否则 Grails 将无法自动了解如何从自定义 XML 格式将数据绑定到域类中。 有关更多信息，请参阅“自定义资源绑定”部分。

容器呈现器

`grails.rest.render.ContainerRenderer` 是一个用于呈现对象容器（列表、映射、集合等）响应的呈现器。 该接口与 `Renderer` 接口几乎相同，只是添加了 `getComponentType()` 方法，该方法应返回“包容”的类型。 例如：

```groovy
class BookListRenderer implements ContainerRenderer<List, Book> {
    Class<List> getTargetType() { List }
    Class<Book> getComponentType() { Book }
    MimeType[] getMimeTypes() { [MimeType.XML] as MimeType[] }
    void render(List object, RenderContext context) {
        ....
    }
}
```