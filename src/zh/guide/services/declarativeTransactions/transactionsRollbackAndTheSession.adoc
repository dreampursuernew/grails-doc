=== 理解事务和 Hibernate 会话

在使用事务时，您必须考虑到 Hibernate 如何处理底层持久性会话的重要考虑因素。当事务被回滚时，被 GORM 使用的 Hibernate 会话被清除。这意味着会话中的任何对象都变为脱离状态，访问未初始化的延迟加载集合将导致 `LazyInitializationException` 。

要理解清除 Hibernate 会话的重要性，考虑以下示例：

```groovy
class Author {
    String name
    Integer age

    static hasMany = [books: Book]
}
```

如果您使用连续事务保存两个作者如下：

```groovy
Author.withTransaction { status ->
    new Author(name: "Stephen King", age: 40).save()
    status.setRollbackOnly()
}

Author.withTransaction { status ->
    new Author(name: "Stephen King", age: 40).save()
}
```

只有第二位作者会被保存，因为第一个事务通过清除 Hibernate 会话来回滚作者的 `save()` 。如果 Hibernate 会话没有被清除，那么两个作者实例都将被持久化，并将导致非常意外的结果。

然而，由于清除了会话而导致 `LazyInitializationException` ，可能会让人感到沮丧。

例如，考虑以下示例：

```groovy
class AuthorService {

    void updateAge(id, int age) {
        def author = Author.get(id)
        author.age = age
        if (author.isTooOld()) {
            throw new AuthorException("太老了", author)
        }
    }
}
```

```groovy
class AuthorController {

    def authorService

    def updateAge() {
        try {
            authorService.updateAge(params.id, params.int("age"))
        }
        catch(e) {
            render "作者作品 ${e.author.books}"
        }
    }
}
```

在上述示例中，如果 `Author` 的年龄超过了 `isTooOld()` 方法中定义的最大值，事务将被回滚并抛出 `AuthorException` 。 `AuthorException` 引用了作者，但当访问 `books` 关联时，会抛出 `LazyInitializationException` ，因为底层的 Hibernate 会话已被清除。

要解决这个问题，您有几个选项。其中一个是确保您进行急切查询，以获取所需的数据：

```groovy
class AuthorService {
    ...
    void updateAge(id, int age) {
        def author = Author.findById(id, [fetch:[books:"eager"]])
        ...
```

在这个示例中，在检索 `Author` 时将查询 `books` 关联。

注意：这是最佳解决方案，因为它需要比以下建议的解决方案更少的查询。

另一个解决方案是在事务回滚后重定向请求：

```groovy
class AuthorController {

    AuthorService authorService

    def updateAge() {
        try {
            authorService.updateAge(params.id, params.int("age"))
        }
        catch(e) {
            flash.message = "无法更新年龄"
            redirect action:"show", id:params.id
        }
    }
}
```

在这种情况下，一个新请求将处理重新检索 `Author` 。最后一个解决方案是再次检索 `Author` 数据，以确保会话保持在正确的状态：

```groovy
class AuthorController {

    def authorService

    def updateAge() {
        try {
            authorService.updateAge(params.id, params.int("age"))
        }
        catch(e) {
            def author = Author.read(params.id)
            render "作者作品 ${author.books}"
        }
    }
}
```

=== 验证错误和回滚

一个常见的用例是如果存在验证错误，则回滚事务。例如，请考虑以下服务：

```groovy
import grails.validation.ValidationException

class AuthorService {

    void updateAge(id, int age) {
        def author = Author.get(id)
        author.age = age
        if (!author.validate()) {
            throw new ValidationException("作者无效", author.errors)
        }
    }
}
```

要重新呈现事务回滚的相同视图，您可以在呈现之前将错误与刷新的实例重新关联：

```groovy
import grails.validation.ValidationException

class AuthorController {

    def authorService

    def updateAge() {
        try {
            authorService.updateAge(params.id, params.int("age"))
        }
        catch (ValidationException e) {
            def author = Author.read(params.id)
            author.errors = e.errors
            render view: "edit", model: [author:author]
        }
    }
}
```