在领域类中，[constraints]是使用约束属性通过分配一个代码块来定义的：

```groovy
class User {
    String login
    String password
    String email
    Integer age

    static constraints = {
      ...
    }
}
```

然后，您可以使用与约束适用的属性名称匹配的方法调用，以及命名参数来指定约束：

```groovy
class User {
    ...

    static constraints = {
        login size: 5..15, blank: false, unique: true
        password size: 5..15, blank: false
        email email: true, blank: false
        age min: 18
    }
}
```

在这个例子中，我们声明了`login`属性必须在5到15个字符之间，不能为空并且必须是唯一的。我们还应用了其他约束到`password`，`email`和`age`属性。

注意：默认情况下，所有领域类属性都不可为空（即具有隐式的`nullable: false`约束）。

可在快速参考部分的约束标题下找到可用约束的完整参考。

请注意，约束只评估一次，这对于依赖于值的约束（例如`java.util.Date`的实例）可能是相关的。

```groovy
class User {
    ...

    static constraints = {
        // 当评估约束时，此Date对象被创建，而不是每次验证User类的实例时。
        birthDate max: new Date()
    }
}
```

=== 警告 - 从约束引用领域类属性

很容易尝试从静态约束块中引用实例变量，但在Groovy（或Java）中这是不合法的。 如果这样做，您将因此受到`MissingPropertyException`。 例如，您可能尝试

```groovy
class Response {
    Survey survey
    Answer answer

    static constraints = {
        survey blank: false
        answer blank: false, inList: survey.answers
    }
}
```

看到`inList`约束如何引用实例属性`survey`？ 这不起作用。 相反，请使用一个自定义[验证程序]：

```groovy
class Response {
    ...
    static constraints = {
        survey blank: false
        answer blank: false, validator: { val, obj -> val in obj.survey.answers }
    }
}
```

在这个例子中，自定义验证程序的`obj`参数是正在验证的领域_实例_，所以我们可以访问它的`survey`属性并返回一个布尔值，指示`answer`属性的新值`val`是否有效。